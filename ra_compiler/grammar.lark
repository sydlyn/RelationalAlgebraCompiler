// ra_compiler/grammar.lark
// defines the grammar for the relational algebra compiler as described in [doc file] <- TODO

start: "(" _expr ")"

_expr: unary_ops
     | set_ops
     | join_ops
     | "(" _expr ")"


// ~~~~~~~~ UNARY OPERATIONS ~~~~~~~~ //

unary_ops: projection
         | selection
         | group
         | rename
         | remove_duplicates
         | sort

projection: PROJ_PREFIX "{" attributes "}" table
selection: _SEL_PREFIX "{" comp_cond "}" table
group: _GROUP_PREFIX "{" attributes ";" aggr_conds "}" table
rename: _RENAME_PREFIX "{" CNAME "}" table
remove_duplicates: _REMOVE_DUPLICATES_PREFIX table
sort: _SORT_PREFIX "{" sort_attributes"}" table  

PROJ_PREFIX: "/projection"
           | "/projection^d"
           | "/pi"
           | "/pi^d"

_SEL_PREFIX: "/selection"
           | "/sigma"
           | "/select"

_GROUP_PREFIX: "/group_by"
             | "/group"
             | "/gamma"

_RENAME_PREFIX: "/rename"
             | "/rho"

_REMOVE_DUPLICATES_PREFIX: "/remove_duplicates"
                        | "/remove_dups"
                        | "/delta"

_SORT_PREFIX: "/sort"
           | "/order"
           | "/tau"


// ~~~~~~~~ SET OPERATIONS ~~~~~~~~ //

set_ops: union
       | intersection
       | difference

union: table UNION_PREFIX table // ("," table)*
intersection: table INTERSECT_PREFIX table // ("," table)*
difference: table DIFFERENCE_PREFIX table // ("," table)*

UNION_PREFIX: "/union"
            | "/u"
            | "/union^d"
            | "/u^d"

INTERSECT_PREFIX: "/intersect"
                   | "/inter"
                   | "/intersect^d"
                   | "/inter^d"

DIFFERENCE_PREFIX: "/difference"
                 | "/diff"
                 | "/difference^d"
                 | "/diff^d"


// ~~~~~~~~ JOIN OPERATIONS ~~~~~~~~ //

join_ops: cross
        | join
        | divide

cross: table _CROSS_PREFIX table
join: table JOIN_PREFIX ("{" (attributes | comp_cond) "}")? table
divide: table _DIVIDE_PREFIX table

_CROSS_PREFIX: "/cross_product"
            | "/cross"
            | "/x"i

JOIN_PREFIX: /\/join/
            | /\/inner_join/
            | /\/left_outer_join/
            | /\/left_join/
            | /\/left_outer/
            | /\/right_outer_join/
            | /\/right_join/
            | /\/right_outer/
            | /\/full_outer_join/
            | /\/full_join/
            | /\/full_outer/
            | /\/left_semi(_join)?/
            | /\/left_semi/
            | /\/right_semi_join/
            | /\/right_semi/

_DIVIDE_PREFIX: "/divide"
              | "/div"

// ~~~~~~~~ TABLES, ATTRIBUTES, & OTHER ~~~~~~~~ //

table: CNAME | "(" _expr ")"

attributes: attr ("," attr)*
sort_attributes: attr SORT_DIR ("," attr SORT_DIR)*

?attr: CNAME ("." CNAME)?
     | math_cond "->" CNAME     // TODO: fix to be communitive
     | CNAME "->" CNAME

math_cond: attr MATH_OP (attr | NUMBER)  // TODO: fix to be communitive
        | "(" math_cond ")"
        
// TODO: fix to not allow NUMBER COMP_OP NUMBER but still be communitive
comp_cond: _comp_cond_attr COMP_OP _comp_cond_attr 
          | "(" comp_cond ")"
          | comp_cond (AND | OR) comp_cond

_comp_cond_attr: (attr | NUMBER | ESCAPED_STRING | math_cond) 

aggr_conds: aggr_func ("," aggr_func)*
aggr_func: AGGR_OP "(" attr ")"

MATH_OP: "+" | "-" | "*" | "/" | "%" | "^"
COMP_OP: ">" | "<" | ">=" | "<=" | "!=" | "==" | "="
AGGR_OP: "sum"i | "count"i | "avg"i | "min"i | "max"i
AND: "and"i | "&&" | "&" 
OR: "or"i | "||" | "|"
SORT_DIR: "asc"i | "desc"i

%import common.ESCAPED_STRING
%import common.CNAME
%import common.NUMBER
%import common.WS
%ignore WS
